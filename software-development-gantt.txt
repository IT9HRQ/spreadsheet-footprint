
# Start date cell formula
= IF(
    MIN(D3:D)>DATEVALUE("1/1/2010");
    MIN(D3:D);
    DATEVALUE("1/1/2010")
  )

# End date cell formula
= IF(
    MAX(E3:E)>$D$2;
    MAX(E3:E);
    $D$2
  )

# End date column formula
= IF(OR(B3=""; C3=0; D3=""); ""; D3+C3)

# Months row
= IF(
    DAY(COLUMN()+$D$2-6) = 1; 
    LEFT(TEXT(COLUMN()+$D$2-6; "MMMM"); 2); 
    ""
  )

# Days row formula
= DAY(COLUMN()+$D$2-6)

# Days row conditional rule
= OR(
    WEEKDAY(COLUMN()+$D$2-6)=1,
    WEEKDAY(COLUMN()+$D$2-6)=7
  )

# Gantt area task conditional rule
= AND(
    NOT(INDEX($A$3:$E$100; ROW()-2; 1)=INT(INDEX($A$3:$E$100; ROW()-2; 1)));
    NOT(INDEX($A$3:$E$100; ROW()-2; 4)="");
    NOT(INDEX($A$3:$E$100; ROW()-2; 5)="");
    INDEX($A$3:$E$100; ROW()-2; 5)>(COLUMN()+$D$2-6);
    INDEX($A$3:$E$100; ROW()-2; 4)<=(COLUMN()+$D$2-6)
  )

# Gantt area milestone conditional rule
= AND(
    INDEX($A$3:$E$100; ROW()-2; 1)=INT(INDEX($A$3:$E$100; ROW()-2; 1));
    NOT(INDEX($A$3:$E$100; ROW()-2; 4)="");
    NOT(INDEX($A$3:$E$100; ROW()-2; 5)="");
    INDEX($A$3:$E$100; ROW()-2; 5)>(COLUMN()+$D$2-6);
    INDEX($A$3:$E$100; ROW()-2; 4)<=(COLUMN()+$D$2-6)
  )

# Milestone bold conditional rule
= AND(
    NOT(INDEX($A$3:$A$50; ROW()-2; 1)="");
    INT(INDEX($A$3:$A$50; ROW()-2; 1))=INDEX($A$3:$A$50; ROW()-2; 1)
)


#
= INDEX($D$3:$E$100;1;1)>=(COLUMN()-6+$D$2))

=INDEX($D$3:$E$100,ROW()-2,1)>=DATE(COLUMN()-6+$D$2)


